%	$Header: solutions.pl,v 1.2 85/10/12 22:33:40 lee Exp $
%
%	Logical all solutions pred solutions/3 is transformed (trf.pl)
%	into $solutions/4, the first arg being the global vars.
%
%	This is still rather hacky and needs to be cleaned up.  It may even
%	have a bug or two.  Still relies on MU-Prolog stuff.
%
%	Error messages are printed if there are local variables in answers or
%	delayed calls in the goals.
%	Noteq and term_compare(?) should be implemented in C, not Prolog -
%	this version is far slower than it could be
%
%						Lee Naish

%?- hide([tsort(2), tinsert(3), prove(1), $bag]).

$solutions(G, X, P, Set) :-
	$bagof(X, P, G, Bag),
	tsort(Bag, Set).

$bagof(X, P, Key, Bag) :-
	asserta(bagtmp($bag)),
	prove(P),
	asserta(bagtmp(Key - X)),
	make_ground(Key),
	not ground(X),
	writeln(2, 'Error: unbound local variables in solutions'),
	fail.
$bagof(X, P, Key, Bag) :-
	$reap([], Bags),
	$pick(Bags, Key, Bag).

$nonempty(_._).

$reap(L0, L) :-
	retract(bagtmp(X)),
	!,
	$reap1(X, L0, L).

$reap1(X, L0, L) :-
	X \== $bag,
	!,
	$reap(X.L0, L).
$reap1(_, L, L).

$pick([], _, []).
$pick(Key-T1.Tn, Key, T1.S) :-
	$pick(Tn, Key, S).
$pick(K-T1.Tn, Key, S) :-
	noteq(Key, K),		% all K Key ~= K,
	$pick(Tn, Key, S).

prove(P) :-		% instead of call, which is effected by cut
	$ndelay(N),
	P,
	$ndelay(M),
	(if N =\= M then
		writeln('Error: delayed call(s) in solutions')
	).

	% should write a more efficient sort (not O(N*N))
tsort([], []).
tsort(A.B, C.D) :-
	tsort(B, E),
	tinsert(A, E, C.D).

tinsert(A, [], [A]).
tinsert(A, B.C, D) :-
	term_compare(R, A, B),
	(	R = (<),
		D = A.B.C
	;
		R = (=),
		tinsert(A, C, D)
	;
		R = (>),
		D = B.E,
		tinsert(A, C, E)
	).

%?- protect([$setof(3), $bagof(3), $bagof(4), $nonempty(1), $reap(2), $reap1(3),
	%$pick(3), $excess_vars(4), $rem_excess_vars(5), $introduce(3)]).

% should use $noteq/3 eventually

	% noteq(X, Y) means for all Y, X \= Y
	% needs the subsumes1 predicate from subsumes.pl

noteq(Key, K) :-		% for all vars in K, K <> Key
	$lhv([Key], V, _),	% V,_ is a d-list of vars in Key
	$noteq1(Key, V, K).

$noteq1(Key, [], Key) :-		% no vars, equal -> fail
	!,
	fail.
$noteq1(Key, V, K) :-		% dont unify -> succeed
	Key \= K,
	!.
$noteq1(Key, V, K) :-		% K as general as Key -> fail
	subsumes1(K, Key),
	!,
	fail.
$noteq1(Key, V, K) :-		% wait for a var to be bound, then try again
	$lhv(V, V1, _),
	!,
	$lfreeze1(V1, $noteq1(Key, V1, K)).

%?- hide(u(1)).

?- wait $lfreeze1(0, 0).
$lfreeze1(L, _) :-	% succeeds if L is a single var
	L = [],
	!.
			% causes delay if arg is a list of distinct vars
$lfreeze1(u(0).u(1).u(2).u(3).u(4).u(5).u(6).u(7).u(8).u(9).u(10).u(11).u(
		12).u(13).u(14).u(15).u(16).u(17).u(18).u(19).[], _).
$lfreeze1(_, G) :-
	call(G).

%?- protect([$noteq1(3), $lfreeze1(2)]).

%?- ['subsumes.pl', 'compare.pl', 'ground.pl'].
?- lib subsumes, lib compare, lib ground.
